/*
 * Copyright 2012-2014 One Platform Foundation
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
        versionCode 2
        versionName "2.0"

        applicationId 'org.onepf.opfpush.sample'

        manifestPlaceholders = [packageId: "\${applicationId}".toString()]
    }
}

dependencies {
    compile project(':providers:gcm')
    //noinspection GradleDependency
    compile 'com.google.android.gms:play-services:5.0.89'
    compile 'com.jakewharton:butterknife:5.1.2'
    compile 'com.android.support:appcompat-v7:19.1.0'
}

afterEvaluate { project ->
    android.applicationVariants.each { variant ->
        variant.javaCompile.dependsOn stripPlayServices
    }
}

task stripPlayServices << {
    def playServiceRootFolder = new File(rootProject.buildDir, "intermediates/exploded-aar/com.google.android.gms/play-services/")
    playServiceRootFolder.list().each { versionName ->
        def versionFolder = new File(playServiceRootFolder, versionName)
        copy {
            from (file(new File(versionFolder, "classes.jar")))
            into (file(versionFolder))
            rename { fileName ->
                fileName = "classes_orig.jar"
            }
        }
        tasks.create(name: "strip" + versionName, type: Jar) {
            destinationDir = versionFolder
            archiveName = "classes.jar"
            from (zipTree(new File(versionFolder, "classes_orig.jar"))) {
                exclude "com/google/ads/**"
                exclude "com/google/android/gms/ads/**"
                exclude "com/google/android/gms/analytics/**"
                exclude "com/google/android/gms/appindexing/**"
//                exclude "com/google/android/gms/appstate/**"
                exclude "com/google/android/gms/cast/**"
                exclude "com/google/android/gms/drive/**"
//                exclude "com/google/android/gms/dynamic/**"
                exclude "com/google/android/gms/games/**"
                exclude "com/google/android/gms/location/**"
                exclude "com/google/android/gms/maps/**"
                exclude "com/google/android/gms/panorama/**"
                exclude "com/google/android/gms/plus/**"
                exclude "com/google/android/gms/security/**"
                exclude "com/google/android/gms/tagmanager/**"
                exclude "com/google/android/gms/wallet/**"
                exclude "com/google/android/gms/wearable/**"
            }
        }.execute()
        delete {
            delete (file(new File(versionFolder, "classes_orig.jar")))
        }
    }
}
